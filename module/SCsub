#!/usr/bin/env python

import glob

Import('env')

sources = glob.glob("*.cpp")

procedured_dir = "#thirdparty/procedured"
procedured_source_files = [
    "Graph.cpp",
    "DepthFirstMazeGenerator.cpp"
]

procedured_sources = [
    procedured_dir + "/src/" + file for file in procedured_source_files
]

sources += procedured_sources

env_labyrinth = env.Clone()
env_labyrinth.Append(CXXFLAGS=["-O2", "-std=c++11", "-Werror", "-Wall"])
env_labyrinth.Append(CPPPATH=[procedured_dir + "/include","./"])

if ARGUMENTS.get("labyrinth_shared", "no") == "yes":
    env_labyrinth.Append(CXXFLAGS="-fPIC")

    env_labyrinth['LIBS'] = []

    shared_lib = env_labyrinth.SharedLibrary(target="#bin/labyrinth", source=sources)


    shared_lib_shim = shared_lib[0].name.rsplit(".", 1)[0]
    env.Append(LIBS=[shared_lib_shim])
    env.Append(LIBPATH="#bin")

else:
    env_labyrinth.add_source_files(env.modules_sources, sources)
